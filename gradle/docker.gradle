/**
 * Docker tasks
 */

task dockerPrepareEnv(type: Copy) {
    from project.properties.dockerDir
    include 'ontrack-ui-*.jar'
    exclude '*-javadoc.jar'
    exclude '*-sources.jar'
    into project.file('docker')
    rename '.*', 'ontrack.jar'
}

// In case of regular (local) build, we want dockerPrepareEnv to run after ':ontrack-ui:bootRepackage'
def ontrackUi = rootProject.findProject(':ontrack-ui')
if (ontrackUi && ontrackUi.tasks.findByName('bootRepackage')) {
    dockerPrepareEnv.mustRunAfter ':ontrack-ui:bootRepackage'
}

task dockerBuild(type: Exec, dependsOn: dockerPrepareEnv) {
    executable 'docker'
    args = [
            'build',
            '--tag',
            "nemerosa/ontrack:${ontrackVersion}",
            project.file('docker')
    ]
}

task dockerLatest(type: Exec, dependsOn: dockerBuild) {
    executable 'docker'
    args = [
            'tag',
            '--force',
            "nemerosa/ontrack:${ontrackVersion}",
            "nemerosa/ontrack:latest",
    ]
}
